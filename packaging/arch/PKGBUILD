# Maintainer: sw1nn <git@sw1nn.com>

pkgname=verandah-plugin-pomodoro
pkgver=0.1.0
pkgrel=1
pkgdesc='Pomodoro timer plugin for verandah Stream Deck daemon'
arch=('x86_64')
url=https://github.com/sw1nn/verandah-plugin-pomodoro
license=('MIT')
depends=('alsa-lib')
makedepends=('rust' 'clang' 'pkg-config')
source=("${pkgname}::git+ssh://git.sw1nn.net/sw1nn/verandah-plugin-pomodoro?tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd ${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}

  export RUSTFLAGS="--remap-path-prefix=$srcdir=/usr/src/debug/$pkgname"
  cargo build --release --locked --offline
}

check() {
  cd ${pkgname}
  cargo test --locked --offline
}

package() {
  cd ${pkgname}

  # Install plugin library
  install -Dm0755 "${CARGO_TARGET_DIR:-target}/release/libverandah_plugin_pomodoro.so" \
    "$pkgdir/usr/lib/verandah/plugins/pomodoro.so"

  # Install control binary
  install -Dm0755 "${CARGO_TARGET_DIR:-target}/release/verandah-pomodoroctl" \
    "$pkgdir/usr/bin/verandah-pomodoroctl"

  # Install shell completions
  local completions_dir="${CARGO_TARGET_DIR:-target}/release/completions"

  # Bash completions
  install -Dm0644 "$completions_dir/verandah-pomodoroctl.bash" \
    "$pkgdir/usr/share/bash-completion/completions/verandah-pomodoroctl"

  # Zsh completions
  install -Dm0644 "$completions_dir/_verandah-pomodoroctl" \
    "$pkgdir/usr/share/zsh/site-functions/_verandah-pomodoroctl"

  # Fish completions
  install -Dm0644 "$completions_dir/verandah-pomodoroctl.fish" \
    "$pkgdir/usr/share/fish/vendor_completions.d/verandah-pomodoroctl.fish"
}
